import java.nio.charset.StandardCharsets

plugins {
    id 'java'
    id 'com.github.davidmc24.gradle.plugin.avro' version '1.9.1'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
}

apply from: "$rootDir/dependencies.gradle"

group 'net.uweeisele.kstreams'
version '0.1-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

compileJava {
    options.release = 21
    options.encoding = StandardCharsets.UTF_8
}

avro {
    fieldVisibility = "PRIVATE"
    outputCharacterEncoding = StandardCharsets.UTF_8
}

dependencies {
    implementation libs.kafkaStreams
    implementation libs.kafkaStreamsAvroSerde
    implementation libs.avro
    implementation libs.commonsLang
    implementation libs.awaitility

    runtimeOnly libs.slf4jlog4j

    testImplementation libs.kafkaStreamsTestUtils
    testImplementation libs.assertj
    testImplementation libs.junitApi
    testRuntimeOnly libs.junitEngine

    // required for integration tests
    testImplementation libs.kafka
    testImplementation libs.kafka_test
    testImplementation libs.kafka_test_sources // required, because intellij does not automatically detect test sources
    testImplementation libs.kafka_server_common_test
    testImplementation libs.kafka_server_common_test_sources // required, because intellij does not automatically detect test sources
    testImplementation libs.kafkaClients_test
    testImplementation libs.kafkaClients_test_sources // required, because intellij does not automatically detect test sources
    testRuntimeOnly libs.bouncycastle // for ZK test
}

configurations.all {
    resolutionStrategy {
        // required because avro serde includes Confluent Kafka
        force libs.kafkaClients
        force libs.kafka
        // check for updates every build for snapshot versions
        cacheChangingModulesFor 12, 'hours'
    }
}

test {
    useJUnitPlatform()

    systemProperty "java.io.tmpdir", "$buildDir/tmp"

    testLogging {
        outputs.upToDateWhen { false }
        showStandardStreams = true
        exceptionFormat = "full"
    }
}
